;--------------------------------------------------
; 【連想配列】Dictionaryモジュール
; サンプルファイル(エイリアス無効化)
; https://github.com/yosgspec/DictionaryOnHSP
;--------------------------------------------------

#cmpopt varinit 1
#define DictionaryAliasOff
#include "Dictionary.as"
#define global ctype dc(%1) %1@Dictionary

#module Program
	#deffunc newForm
		screen 0,380,240,0
		cls 1
		title "DictionaryPreview"
		objmode 2

		pos 10,0: mes "Key"
		pos 10,20
		key_txt=""
		input key_txt,120,30

		pos 10,50: mes "Keys"
		pos 10,70
		sdim keys_txt
		mesbox keys_txt,120,160,1
		keys_id=stat

		pos 130,0: mes "Value"
		pos 130,20
		value_txt=""
		input value_txt,120,30
		value_id=stat

		pos 130,50: mes "Values"
		pos 130,70
		sdim values_txt
		mesbox values_txt,120,160,1
		values_id=stat

		pos 250,0: mes "AddValue"
		objsize 120,30
		pos 250,20
		addValue_txt=""
		input addValue_txt,120,30
		addValue_id=stat

		pos 250,50
		button gosub "SetItem",*setItem_btn
		pos 250,80
		button gosub "AddItem",*addItem_btn
		pos 250,110
		button gosub "GetItem",*getItem_btn
		pos 250,140
		button gosub "AddValue",*addValue_btn
		pos 250,170
		button gosub "RemoveItem",*removeItem_btn
		pos 250,200
		button gosub "ViewAll",*viewAll_btn
	return

	#deffunc main
		newForm
		new@Dictionary dict,"str",,,{"
			name:bob,
			age:40,
			hobby:読書,
			work:平社員
		"}
		reload
	return

	#deffunc reload
		sdim keys
		sdim values
		dc(foreach) dict
			keys+=dc(keys)(dict,cnt)+"\n"
			values+=dc(values)(dict,cnt)+"\n"
		loop
		objprm keys_id,keys
		objprm values_id,values
	return

	*setItem_btn
		dc(set) dict,key_txt,value_txt
		reload
	return

	*addItem_btn
		dc(add) dict,key_txt,value_txt
		if stat {
			dialog "AddError!"
			return
		}
		reload
	return

	*getItem_btn
		value=dc(item)(dict,key_txt)
		if value=dc(null) {
			dialog "NotContainsKey!"
			return
		}
		objprm value_id,value
	return

	*addValue_btn
		dc(reSet) dict,key_txt,"+",addValue_txt
		if stat {
			dialog "AddValueError!"
			return
		}
		reload
		objprm value_id,dc(item)(dict,key_txt)
	return

	*removeItem_btn
		dc(remove) dict,key_txt
		if stat {
			dialog "RemoveError!"
			return
		}
		reload
	return

	*viewAll_btn
		dialog dc(joinDict)(dict)
	return
#global
main
stop